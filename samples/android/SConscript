# Copyright 2010 - 2011, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
# 

Import('env')

# Application samples
for sample in ['simple/client', 
               'simple/service', 
               'secure/logonclient', 
               'secure/rsaclient', 
               'secure/srpclient', 
               'secure/service', 
               'chat',
               'properties/PropertiesClient',
               'properties/PropertiesService',
               'contacts/ContactsClient',
               'contacts/ContactsService']:
    env.Install('$DISTDIR/samples/%s' % sample, 
                ['%s/AndroidManifest.xml' % sample,
                 '%s/.classpath' % sample,
                 '%s/default.properties' % sample,
                 '%s/.project' % sample,
                 '%s/res' % sample,
                 '%s/src' % sample])
    env.Install('$DISTDIR/samples/%s/libs' % sample, '$JARDIR/alljoyn.jar')
    env.Install('%s/libs' % sample, '$JARDIR/alljoyn.jar')
    if env['OS'] == 'android' and env['CPU'] == 'arm5':
        env.Install('$DISTDIR/samples/%s/libs/armeabi' % sample, '$DISTDIR/lib/liballjoyn_java.so')
        #make sure the samples are using the most recent version of the library
        env.Install('%s/libs/armeabi' % sample, '$DISTDIR/lib/liballjoyn_java.so')
    elif env['OS'] == 'android' and env['CPU'] == 'arm':
        env.Install('$DISTDIR/samples/%s/libs/armeabi-v7a' % sample, '$DISTDIR/lib/liballjoyn_java.so')
        env.Install('%s/libs/armeabi-v7a' % sample, '$DISTDIR/lib/liballjoyn_java.so')

env.Install('$DISTDIR/samples', 'README.txt')

# Unit test runner
build_dir = '#build/${OS}/${CPU}/${VARIANT}'
env.Install(build_dir + '/test', 
            ['test/AndroidManifest.xml',
             'test/.classpath',
             'test/default.properties',
             'test/.project',
             'test/res',
             'test/src'])
env.Install(build_dir + '/test/libs', '$JARDIR/alljoyn.jar')
env.Install(build_dir + '/test/libs', '$JARDIR/alljoyn_test.jar')
if env['OS'] == 'android' and env['CPU'] == 'arm5':
    env.Install(build_dir + '/test/libs/armeabi', '$DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs/armeabi', '$DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs', '$JARDIR/alljoyn.jar')
    env.Install('./test/libs', '$JARDIR/alljoyn_test.jar')
elif env['OS'] == 'android' and env['CPU'] == 'arm':
    env.Install(build_dir + '/test/libs/armeabi-v7a', '$DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs/armeabi-v7a', '$DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs', '$JARDIR/alljoyn.jar')
    env.Install('./test/libs', '$JARDIR/alljoyn_test.jar')
   
 
