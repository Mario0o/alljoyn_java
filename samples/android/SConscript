# Copyright 2010 - 2011, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
# 

Import('env')

# Application samples
for sample in ['simple/client', 
               'simple/service', 
               'simple/wfd_service',
               'chat', 
               'raw/client', 
               'raw/service', 
               'secure/logonclient', 
               'secure/rsaclient', 
               'secure/srpclient', 
               'secure/service', 
               'properties/PropertiesClient',
               'properties/PropertiesService',
               'contacts/ContactsClient',
               'contacts/ContactsService']:
    env.Install('$JAVA_DISTDIR/samples/%s' % sample, 
                ['%s/AndroidManifest.xml' % sample,
                 '%s/.classpath' % sample,
                 '%s/default.properties' % sample,
                 '%s/.project' % sample,
                 '%s/res' % sample,
                 '%s/src' % sample])
    env.Install('$JAVA_DISTDIR/samples/%s/libs' % sample, '$JARDIR/alljoyn.jar')
    env.Install('%s/libs' % sample, '$JARDIR/alljoyn.jar')
    if env['OS'] == 'android' and env['CPU'] == 'arm':
        env.Install('$JAVA_DISTDIR/samples/%s/libs/armeabi' % sample, '$JAVA_DISTDIR/lib/liballjoyn_java.so')
        env.Install('%s/libs/armeabi' % sample, '$JAVA_DISTDIR/lib/liballjoyn_java.so')

env.Install('$JAVA_DISTDIR/samples', 'README.txt')

# install the alljoyn_java.jar and liballjoyn_java.so into the shared 
# keystore samples at this time the shared keystore samples are not being placed
# in the dist folder if they are the samples could be moved into the Application 
# samples loop shown above.
for sample in ['secure/sharedks/service',
               'secure/sharedks/srpclientA',
               'secure/sharedks/srpclientB']:
    if env['OS'] == 'android' and env['CPU'] == 'arm':
        env.Install('%s/libs' % sample, '$JARDIR/alljoyn.jar')
        env.Install('%s/libs/armeabi' % sample, '$JAVA_DISTDIR/lib/liballjoyn_java.so')

# Unit test runner
env.Install('$JAVA_TESTDIR',
            ['test/AndroidManifest.xml',
             'test/.classpath',
             'test/default.properties',
             'test/.project',
             'test/res',
             'test/src'])
env.Install('$JAVA_TESTDIR' + '/libs', '$JARDIR/alljoyn.jar')
env.Install('$JAVA_TESTDIR' + '/libs', '$JARDIR/alljoyn_test.jar')
if env['OS'] == 'android' and env['CPU'] == 'arm':
    env.Install('$JAVA_TESTDIR' + '/libs/armeabi', '$JAVA_DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs/armeabi', '$JAVA_DISTDIR/lib/liballjoyn_java.so')
    env.Install('./test/libs', '$JARDIR/alljoyn.jar')
    env.Install('./test/libs', '$JARDIR/alljoyn_test.jar')

 
